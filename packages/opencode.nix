{ pkgs, system }:

let
  version = "0.5.7";

  architectures = {
    "x86_64-linux" = "linux-x64";
    "aarch64-linux" = "linux-arm64";
    "x86_64-darwin" = "darwin-x64";
    "aarch64-darwin" = "darwin-arm64";
  };
  arch = architectures.${system} or (throw "unsupported system: ${system}");

  checksums = {
    "opencode-ai" = "1kfs0h993ddzs988gnsav4fh5xjpv55byn06h4ajdgqf6ydrpyzz";
    "opencode-darwin-arm64" = "1imjcy2747x7l3mlw4mahkmbi14ac84gbwip60d7r7scn18cqrxl";
    "opencode-darwin-x64" = "1q6z479m6jc9024a14qd99gl6pxwncznm3ws2hq4mw0n34cvx5gw";
    "opencode-linux-arm64" = "1x1nr7z7c4g0971x4i8yzql20rasbwb6chh12zf0ssv0xsl28k0q";
    "opencode-linux-x64" = "06gbm0amfmhyj62lgf8gzcrrcnklvn7v73103wmm5ayxwabkiccw";
  };
  opencodeSha = checksums."opencode-ai";
  platformSha = checksums."opencode-${arch}" or (throw "no sha for: opencode-${arch}");

  platformPackage = "opencode-${arch}";
in

pkgs.stdenv.mkDerivation {
  pname = "opencode";
  inherit version;

  src = pkgs.fetchurl {
    url = "https://registry.npmjs.org/opencode-ai/-/opencode-ai-${version}.tgz";
    sha256 = opencodeSha;
  };

  platformSrc = pkgs.fetchurl {
    url = "https://registry.npmjs.org/${platformPackage}/-/${platformPackage}-${version}.tgz";
    sha256 = platformSha;
  };

  nativeBuildInputs = [ pkgs.makeWrapper ];

  installPhase = ''
    mkdir -p $out/{bin,lib/{opencode-ai,${platformPackage}}}
    tar -xzf $src --strip-components=1 -C $out/lib/opencode-ai
    tar -xzf $platformSrc --strip-components=1 -C $out/lib/${platformPackage}
    ln -s $out/lib/${platformPackage}/bin/opencode $out/bin/opencode
    chmod +x $out/bin/opencode
    wrapProgram $out/bin/opencode --set OPENCODE_BIN_PATH $out/lib/${platformPackage}/bin/opencode
  '';

  meta = with pkgs.lib; {
    description = "AI coding agent, built for the terminal.";
    homepage = "https://github.com/sst/opencode";
    license = licenses.mit;
    platforms = [ system ];
    mainProgram = "opencode";
  };
}
